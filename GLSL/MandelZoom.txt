vec3 hsv2rgb(vec3 c)
{
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

void mainImage( out vec4 fragColor, in vec2 fragCoord ) {	
    
	vec2 uv = fragCoord.xy / iResolution.xy;
    uv = uv*2.0 - 1.0;
    uv *= pow(2.0,5.0*sin(iTime*0.3)-5.0);
    uv.x *= iResolution.x / iResolution.y;
    uv += vec2(-1.18, 0.301);
    
    vec2 z = vec2(0.0);
    vec2 c = uv;
    
    float iterations = -1.0;
    for (float i = 0.0; i < 200.0; i++) {
        z = vec2( z.x*z.x - z.y*z.y, 2.0*z.x*z.y) + c;
        if (dot(z,z) > 256.0) {
            iterations = i;
            break;
        }
    }
    
    if (iterations > -1.0)
        iterations = iterations + 2.0 - log(log(length(z)))/log(2.0);
    
    float hue = pow(iterations/50.0,0.4+0.2*sin(0.324*iTime));
    hue += iTime*0.2454;
    
	fragColor = vec4(hsv2rgb(vec3(hue,1.0,1.0)) ,1.0);
    
    if (iterations < 0.0) fragColor = vec4(vec3(0.0),1.0);
    
}
